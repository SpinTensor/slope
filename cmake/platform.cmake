# Copyright (C) 2019  Elvis Teixeira
#
# This source code is free software: you can redistribute it
# and/or modify it under the terms of the GNU Lesser General
# Public License as published by the Free Software Foundation,
# either version 3 of the License, or (at your option) any
# later version.
#
# This source code is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied
# warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
# PURPOSE.  See the GNU Lesser General Public License for
# more details.
#
# You should have received a copy of the GNU Lesser General
# Public License along with this program.
# If not, see <http://www.gnu.org/licenses/>.

##== Detect operating system
if (APPLE)
  set(SLOPE_OS_MAC   1)
  set(SLOPE_OS_NAME "MacOS")
elseif(UNIX)
  set(SLOPE_OS_LINUX   1)
  set(SLOPE_OS_NAME "Linux")
elseif(WIN32)
  set(SLOPE_OS_WINDOWS 1)
  set(SLOPE_OS_NAME "Windows")
endif()

##== Detect compiler and check it's version
set(SLOPE_CC_VERSION ${CMAKE_CXX_COMPILER_VERSION})
if(CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
  set(SLOPE_CC_GCC   1)
  set(SLOPE_CC_NAME "GCC")
  if(CMAKE_CXX_COMPILER_VERSION VERSION_LESS "6.3.0")
    message(FATAL_ERROR "Your version of GCC is too old")
  endif()
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=gnu++14 -fno-builtin-memset")
elseif(CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
  set(SLOPE_CC_CLANG 1)
  set(SLOPE_CC_NAME "Clang")
  if(CMAKE_CXX_COMPILER_VERSION VERSION_LESS "3.8.1")
    message(FATAL_ERROR "Your version of Clang is too old")
  endif()
elseif(CMAKE_CXX_COMPILER_ID STREQUAL "MSVC")
  set(SLOPE_CC_MSVC  1)
  set(SLOPE_CC_NAME "MSVC")
  # Maybe in a few centuries they'll be ready
  if(CMAKE_CXX_COMPILER_VERSION VERSION_LESS "10000.1000.1000")
    message(FATAL_ERROR "Your version of MSVC is too old")
  endif()
endif()

string(CONCAT
  SLOPE_PLATFORM
    "slope ${SLOPE_VERSION_STRING} -"
    " ${CMAKE_SYSTEM_NAME} -"
    " ${SLOPE_CC_NAME} ${SLOPE_CC_VERSION}")

##== DEBUG/RELEASE settings
if (CMAKE_BUILD_TYPE STREQUAL "Debug")
  set(SLOPE_DEBUG_ENABLE 1)
  # Force stats on debug mode
  set(SLOPE_STATS_ENABLE 1)
endif (CMAKE_BUILD_TYPE STREQUAL "Debug")

##== generate platform headers
set(
  SLOPE_AUTOGEN_NOTICE
    "NOTICE: This file is autogenerated, any modifications will be lost!")

configure_file(
  "${CMAKE_SOURCE_DIR}/templates/platform.h"
  "${SLOPE_AUTOGEN_INCLUDE_DIR}/slope/platform.h")

configure_file(
  "${CMAKE_SOURCE_DIR}/templates/platform_p.h"
  "${SLOPE_AUTOGEN_SOURCE_DIR}/slope/platform_p.h")
